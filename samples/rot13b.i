
	DO ,1 <- #1
	DO .4 <- #0
	DO .5 <- #0
	DO COME FROM (30)
	DO WRITE IN ,1
	DO .1 <- ,1SUB#1
	DO (10) NEXT
	PLEASE GIVE UP
(20)	PLEASE RESUME '?.1$#256'~'#256$#256'
(10)	DO (20) NEXT
	DO FORGET #1
	DO .2 <- .4
	DO (1000) NEXT
	DO .4 <- .3~#255
	DO (50) NEXT
	DO .3 <- !3~#15'$!3~#240'
	DO .3 <- !3~#15'$!3~#240'
	DO .2 <- !3~#15'$!3~#240'
	DO .1 <- .5
	DO (1010) NEXT
	DO .5 <- .2
	PLEASE DO ,1SUB#1 <- .3
(30)	PLEASE READ OUT ,1

(50)	PLEASE STASH .1 + .2 + .4 + .5 + .11+.12+.13+.14+.15+.16+.17+.18
	PLEASE DO .5 <- .4
	PLEASE DO .1 <- .5
	DO .2 <- #65
	DO (500) NEXT
	DO .11 <- .3~#2
	DO .2 <- #78
	DO (500) NEXT
	DO .12 <- .3~#2
	DO .2 <- #97
	DO (500) NEXT
	DO .13 <- .3~#2
	DO .2 <- #110
	DO (500) NEXT
	DO .14 <- .3~#2
	PLEASE DO .2 <- .5
	DO .1 <- #77
	DO (500) NEXT
	DO .15 <- .3~#2
	DO .1 <- #90
	DO (500) NEXT
	DO .16 <- .3~#2
	DO .1 <- #109
	DO (500) NEXT
	DO .17 <- .3~#2
	DO .1 <- #122
	DO (500) NEXT
	DO .18 <- .3~#2
	PLEASE DO .1 <- ' "'"!12$.11'~#3"$"!14$.13'~#3"'~#15"
                         $"'"!16$.15'~#3"$"!18$.17'~#3"'~#15" '~#255
	PLEASE DO .1 <- 'V"!&1'~#85"'~#5
	DO .2 <- "!1~#2'$!1~#2'"~#3
	DO .1 <- !2$.1'~#15
	DO .2 <- !2$.2'~#15
	DO .2 <- "!1$.2'$!1$"'"V!2$.1'"~#15'"'"~#65535
	DO .1 <- .5
	PLEASE DO (1009) NEXT 
	PLEASE RETRIEVE .1 + .2 + .4 + .5 + .11+.12+.13+.14+.15+.16+.17+.18
	PLEASE RESUME #1
	
(500)	PLEASE STASH .4
	DO .3 <- "?!1$.2'"~"#0$#65535"
	DO .4 <- .3~.3
	DO .4 <- '&"!?4'$.4"'~'#0$#65535'
	DO .3 <- '"'&"!1~.3'$.4"'~'#0$#65535'"~.4'
	        $'"'&"!2~.3'$.4"'~'#0$#65535'"~.4'
	DO .3 <- "?'"!3~#1'$!3~#2'"$"#1$!3~#1'"'"~#5
	PLEASE RETRIEVE .4
	DO RESUME #1
